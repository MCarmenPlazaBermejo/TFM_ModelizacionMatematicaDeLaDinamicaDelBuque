//Problema de Stokes: Test de la Cavidad 2D (No se consideran fuerzas externas)
//
// Dominio Omega =(0,1)^2
// nu*Laplace u + grad(p)=f=0
//                div(u)=0
// +c.contorno (u,v)=(0,0) en paredes izq. der. e inferior
//            (u,v)=(1,0) en pared superior

//En primer lugar definimos el dominio (cuadrado)
//Malla y espacios de elementos finitos
int nx = 20;
mesh Th=square(nx,nx);
int inferior=1, derecha=2, superior=3, izquierda=4;
plot(Th);

//Elementos finitos Taylor-Hood
fespace Vh(Th, P2); // Espacio para velocidad
fespace Qh(Th, P1);// Espacio para presión

Vh u, v; // Incógnita: velocidad (u,v)
Qh p;    // Incógnita: presión
Vh ub, vb; //Funciones test
Qh pb;     // Función test: presión

real nu=1; // Coeficiente de viscosidad
func f=0; //Estamos considerando a las fuerzas externas 0

macro grad(u) [dx(u),dy(u)] //EOM
macro div(u,v) (dx(u)+dy(v)) //EOM

//vector de incógnitas y vector de funciones test
solve Stokes([u,v,p],[ub,vb,pb]) =
  int2d(Th)(nu*grad(u)'*grad(ub))
  +int2d(Th)( nu*grad(v)'*grad(vb))
  -int2d(Th)( div(ub,vb)*p)
  +int2d(Th)(div(u,v)*pb)
  +int2d(Th)(1.e-10*p*pb) //Penalización de p
  +on(izquierda, inferior, derecha,u=0,v=0)//C.contorno
  +on(superior,u=1, v=0);

//plot([u,v]);
//plot([p]);
plot([u,v], fill=1, value=1);
load "iovtk"
savevtk( "pbStokes.vtu", Th, [ u, v], p, dataname = "Velocidad presion" );
